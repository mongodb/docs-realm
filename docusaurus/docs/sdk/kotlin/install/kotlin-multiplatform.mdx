---
title: Install (Kotlin Multiplatform)
---
import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';
import PrerequisitesKotlin from './_markdown-prerequisites-kotlin.mdx';
import {variables} from '@site/variables';


## Prerequisites

<PrerequisitesKotlin/>

## Installation

<Tabs groupId="sdk-distros">
  <TabItem value="realm-database" label="Realm Database">

To add local-only Realm Database to your application, make
the following changes to your shared module Gradle build
file, typically found at `<project>/shared/build.gradle.kts`:

- Add `io.realm.kotlin:library-base` to the dependencies
  block of your common module.
- Add `io.realm.kotlin` to your list of plugins.
- To use coroutines with the SDK, add
  `org.jetbrains.kotlinx:kotlinx-coroutines-core`
  to the dependencies block of your common module.

<!--- Credit for variable in code blocks solution: https://github.com/facebook/Docusaurus/issues/395#issuecomment-633686061 --->
<pre><code parentName="pre" {...{
            "className": "language-kotlin"
          }}>
{`
plugins {
    kotlin("multiplatform")
    kotlin("native.cocoapods")
    id("com.android.library")
    // highlight-next-line
    id("io.realm.kotlin")
}

version = "1.0"

kotlin {
    android()
    iosX64()
    iosArm64()

    cocoapods {
        summary = "Some description for the Shared Module"
        homepage = "Link to the Shared Module homepage"
        ios.deploymentTarget = "14.1"
        podfile = project.file("../iosRealmKMMApp/Podfile")
        framework {
            baseName = "shared"
        }
    }
    
    sourceSets {
        val commonMain by getting {
            dependencies {
                // highlight-next-line
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${variables.kotlinx_coroutines_version}")
                // highlight-next-line
                implementation("io.realm.kotlin:library-base:${variables.kotlin_sdk_version}")
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
            }
        }
       val androidMain by getting
       val androidTest by getting {
           dependencies {
                implementation(kotlin("test-junit"))
                implementation("junit:junit:4.13.2")
            }
        }
        val iosX64Main by getting
        val iosArm64Main by getting
        
        val iosMain by creating {
            dependsOn(commonMain)
            iosX64Main.dependsOn(this)
            iosArm64Main.dependsOn(this)
        }
        val iosX64Test by getting
        val iosArm64Test by getting
        val iosTest by creating {
            dependsOn(commonTest)
            iosX64Test.dependsOn(this)
            iosArm64Test.dependsOn(this)
        }
    }
}

android {
    compileSdk = 30
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdk = 28
        targetSdk = 30
    }
}
`}
</code></pre>

Then, add `classpath("io.realm.kotlin:gradle-plugin:${variables.kotlin_sdk_version}")`
to the `buildscript.dependencies` block of your project-level
Gradle build file, typically found at `<project>/build.gradle.kts`:

<pre><code parentName="pre" {...{
            "className": "language-kotlin"
          }}>
{`
buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10")
        classpath("com.android.tools.build:gradle:7.1.1")
        // highlight-next-line
        classpath("io.realm.kotlin:gradle-plugin:${variables.kotlin_sdk_version}")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("clean", Delete::class) {
    delete(rootProject.buildDir)
}
`}
</code></pre>

</TabItem>
  <TabItem value="sync" label="Sync">

To add Realm Database and Sync to your application, make
the following changes to your shared module Gradle build
file, typically found at `shared/build.gradle.kts`:

- Add `io.realm.kotlin:library-sync` to the dependencies
  block of your common module.
- Add `io.realm.kotlin` to your list of plugins.
- To use coroutines with the SDK, add
  `org.jetbrains.kotlinx:kotlinx-coroutines-core`
  to the dependencies block of your common module.

<pre><code parentName="pre" {...{
            "className": "language-kotlin"
          }}>
{`
plugins {
    kotlin("multiplatform")
    kotlin("native.cocoapods")
    id("com.android.library")
    // highlight-next-line
    id("io.realm.kotlin")
}

version = "1.0"

kotlin {
    android()
    iosX64()
    iosArm64()

    cocoapods {
        summary = "Some description for the Shared Module"
        homepage = "Link to the Shared Module homepage"
        ios.deploymentTarget = "14.1"
        podfile = project.file("../iosRealmKMMApp/Podfile")
        framework {
            baseName = "shared"
        }
    }
    
    sourceSets {
        val commonMain by getting {
            dependencies {
                // highlight-next-line
                implementation("org.jetbrains.kotlinx:kotlinx-coroutines-core:${variables.kotlinx_coroutines_version}")
                // highlight-next-line
                implementation("io.realm.kotlin:library-sync:${variables.kotlin_sdk_version}")
            }
        }
        val commonTest by getting {
            dependencies {
                implementation(kotlin("test-common"))
                implementation(kotlin("test-annotations-common"))
            }
        }
       val androidMain by getting
       val androidTest by getting {
           dependencies {
                implementation(kotlin("test-junit"))
                implementation("junit:junit:4.13.2")
            }
        }
        val iosX64Main by getting
        val iosArm64Main by getting
        
        val iosMain by creating {
            dependsOn(commonMain)
            iosX64Main.dependsOn(this)
            iosArm64Main.dependsOn(this)
        }
        val iosX64Test by getting
        val iosArm64Test by getting
        val iosTest by creating {
            dependsOn(commonTest)
            iosX64Test.dependsOn(this)
            iosArm64Test.dependsOn(this)
        }
    }
}

android {
    compileSdk = 30
    sourceSets["main"].manifest.srcFile("src/androidMain/AndroidManifest.xml")
    defaultConfig {
        minSdk = 28
        targetSdk = 30
    }
}
`}
</code></pre>


Then, add `io.realm.kotlin:gradle-plugin`
to the `buildscript.dependencies` block of your project-level
Gradle build file, typically found at `<project>/build.gradle.kts`:

<pre><code parentName="pre" {...{
            "className": "language-kotlin"
          }}>
{`
buildscript {
    repositories {
        gradlePluginPortal()
        google()
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10")
        classpath("com.android.tools.build:gradle:7.1.1")
        // highlight-next-line
        classpath("io.realm.kotlin:gradle-plugin:${variables.kotlin_sdk_version}")
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

tasks.register("clean", Delete::class) {
    delete(rootProject.buildDir)
}
`}
</code></pre>

  </TabItem>
</Tabs>

:::note Android Module Dependencies

If you use any part of the SDK inside the Android module, add the
SDK's compile-time dependency to your Android module `build.gradle.kts`:

```kotlin
dependencies {
    compileOnly("io.realm.kotlin:library:${variables.kotlin_sdk_version}")
}
```
:::

After updating the Gradle configuration,
resolve the dependencies by clicking `File` >
`Sync Project with Gradle Files` in the Android Studio menu bar.
You can now use the Kotlin SDK in your application.


