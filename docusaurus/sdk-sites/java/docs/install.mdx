---
title: 'Install'
---
import {variables} from '@site/variables';
import Tabs from '@theme/Tabs';
import TabItem from '@theme/TabItem';

This page details how to install the Realm Java SDK in your project
and get started.

## Prerequisites

- [Android Studio](https://developer.android.com/studio/index.html)
  version 1.5.1 or higher.
- Java Development Kit (JDK) 11 or higher.
- An emulated or hardware Android device for testing.
- Android API Level 16 or higher (Android 4.1 and above).

## Installation

The Java SDK only supports the Gradle build system. Follow these steps
to add the SDK to your project.

:::note ProGuard

Because the SDK provides a ProGuard configuration as part
of the library, you do not need to add any
Realm-specific rules to your ProGuard configuration.
:::

### Project Gradle Configuration

To add local-only Realm Database to your application, make
the following changes to your project-level Gradle build
file, typically found at `<project>/build.gradle`:


<Tabs groupId="gradle-syntax">
  <TabItem value="plugin" label="Gradle Plugin Syntax">

:::tip
The Java SDK does not yet support the Gradle Plugin syntax. Fortunately,
you can still add the SDK to projects that use this syntax.
:::

- Add a `buildscript` block
- Within the `buildscript` block, add a `repositories` block.
- Add the `mavenCentral()` repository to the `buildscript.repositories` block.
- Within the `buildscript` block, add a `dependencies` block.
- Add the `io.realm:realm-gradle-plugin` dependency to the `buildscript.dependencies` block.

<pre><code parentName="pre" {...{
            "language": "groovy"
          }}>
{`
// highlight-start
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "io.realm:realm-gradle-plugin:${variables.java_sdk_version}"
    }
}
// highlight-end

plugins {
    id 'com.android.application' version '7.1.2' apply false
    id 'com.android.library' version '7.1.2' apply false
    id 'org.jetbrains.kotlin.android' version '1.6.10' apply false
    id "org.jetbrains.kotlin.kapt" version "1.6.20" apply false
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
`}
</code></pre>

  </TabItem>
  <TabItem value="legacy" label="Gradle Legacy Syntax">

- Add the `io.realm:realm-gradle-plugin` dependency to the `buildscript.dependencies` block.
- Add the `mavenCentral()` repository to the `allprojects.repositories` block.

<pre><code parentName="pre" {...{
            "language": "groovy"
          }}>
{`
buildscript {
    repositories {
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.5.1"
        // highlight-next-line
        classpath "io.realm:realm-gradle-plugin:${variables.java_sdk_version}"
    }
}

allprojects {
    repositories {
        google()
        // highlight-next-line
        mavenCentral()
    }
    dependencies {
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
`}
</code></pre>

</TabItem>
</Tabs>

### Application Module Gradle Configuration

Then, make the following changes to your application-level
Gradle build file, typically found at `<project>/app/build.gradle`:

<Tabs groupId="gradle-syntax">
  <TabItem value="plugin" label="Gradle Plugin Syntax">

- Apply the `kotlin-kapt` plugin if your application uses Kotlin
- Beneath the `plugins` block, apply the `realm-android` plugin.


<pre><code parentName="pre" {...{
            "language": "groovy"
          }}>
{`
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    // highlight-start
    id 'org.jetbrains.kotlin.kapt'
    // highlight-end
}

// highlight-start
apply plugin: "realm-android"
// highlight-end

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.mongodb.example-realm-application"
        minSdk 28
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation 'io.realm:realm-gradle-plugin:10.10.1'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
`}
</code></pre>

  </TabItem>
  <TabItem value="legacy" label="Gradle Legacy Syntax">


- Apply the `kotlin-kapt` plugin if your application uses Kotlin
- Apply the `realm-android` plugin
- Enable view binding in Android's build features


<pre><code parentName="pre" {...{
            "language": "groovy"
          }}>
{`
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
// highlight-start
apply plugin: 'kotlin-kapt'
apply plugin: 'realm-android'
// highlight-end

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"
    defaultConfig {
        applicationId "com.mongodb.example-realm-application"
        minSdkVersion 21
        targetSdkVersion 29
    }

    // highlight-start
    buildFeatures {
        viewBinding = true
    }
    // highlight-end

    compileOptions {
        sourceCompatibility 1.11
        targetCompatibility 1.11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.core:core-ktx:1.2.0"
}
`}
</code></pre>


  </TabItem>
</Tabs>

After updating the `build.gradle` files, resolve the dependencies by
clicking `File > Sync Project with Gradle Files`.

## Enable Realm Sync

To use Realm Sync in your application, create a `realm` block in your
application module `build.gradle`. Within this block, set `syncEnabled`
to `true`.

<pre><code parentName="pre" {...{
            "language": "groovy"
          }}>
{`
realm {
    syncEnabled = true
}
`}
</code></pre>


## Supported Platforms

The Java SDK supports the following platforms:

- Android
- Wear OS
- Android Automotive OS
- Android TV
- Android Things
